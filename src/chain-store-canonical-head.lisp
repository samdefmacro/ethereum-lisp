(in-package #:ethereum-lisp.core)

(defun engine-payload-store-set-canonical-head
    (store hash &key expected-chain-id chain-config)
  (unless (typep store 'engine-payload-memory-store)
    (block-validation-fail "Engine payload store must be a memory store"))
  (let* ((head-block (engine-payload-store-known-block store hash))
         (previous-head-hash
           (engine-payload-store-canonical-hash
            store
            (engine-payload-memory-store-head-number store)))
         (head-changed-p
           (or (null previous-head-hash)
               (not (hash32= previous-head-hash hash)))))
    (unless head-block
      (block-validation-fail "Canonical head block must be known"))
    (let ((path '()))
      (loop with current = head-block
            do (let* ((header (block-header current))
                      (number (block-header-number header))
                      (current-hash (block-hash current))
                      (current-key (engine-payload-store-key current-hash))
                      (canonical-key
                        (gethash
                         number
                         (engine-payload-memory-store-canonical-hashes store))))
                 (when (and canonical-key
                            (string= canonical-key current-key))
                   (return))
                 (push current path)
                 (when (zerop number)
                   (return))
                 (let* ((parent-hash (block-header-parent-hash header))
                        (parent-block
                          (and parent-hash
                               (engine-payload-store-known-block
                                store parent-hash))))
                   (when (or (null parent-hash)
                             (hash32= parent-hash (zero-hash32)))
                     (return))
                   (unless parent-block
                     (block-validation-fail
                      "Canonical head ancestry must be fully known"))
                   (setf current parent-block))))
      (let ((displaced-blocks '()))
        (dolist (block path)
          (let* ((header (block-header block))
                 (number (block-header-number header))
                 (old-block (engine-payload-store-block-by-number
                             store number)))
            (when (and old-block
                       (not (hash32= (block-hash old-block)
                                     (block-hash block))))
              (push old-block displaced-blocks))))
        (dolist (block path)
          (let* ((header (block-header block))
                 (number (block-header-number header))
                 (key (engine-payload-store-key (block-hash block))))
            (setf (gethash number
                           (engine-payload-memory-store-canonical-hashes store))
                  key
                  (gethash number
                           (engine-payload-memory-store-number-blocks store))
                  block)
            (engine-payload-store-index-block-transactions
             store
             block
             :force t)
            (engine-payload-store-remove-included-block-transactions
             store
             block)))
        (let ((new-head-number
                (block-header-number (block-header head-block)))
              (stale-numbers '()))
          (maphash (lambda (number key)
                     (declare (ignore key))
                     (when (> number new-head-number)
                       (let ((old-block
                               (engine-payload-store-block-by-number
                                store number)))
                         (when old-block
                           (push old-block displaced-blocks)))
                       (push number stale-numbers)))
                   (engine-payload-memory-store-canonical-hashes store))
          (dolist (number stale-numbers)
            (remhash number
                     (engine-payload-memory-store-canonical-hashes store)))
          (setf (engine-payload-memory-store-head-number store) new-head-number
                (engine-payload-memory-store-head-checkpoint store)
                (make-chain-store-checkpoint :label :head :block-hash hash)))
        (engine-payload-store-remove-new-head-invalid-txpool-transactions
         store
         :chain-config chain-config)
        (dolist (block displaced-blocks)
          (engine-payload-store-remove-block-transaction-locations
           store
           block))
        (engine-payload-store-reinsert-displaced-block-transactions
         store
         displaced-blocks
         :expected-chain-id expected-chain-id
         :chain-config chain-config)
        (when head-changed-p
          (dolist (block (sort (copy-list displaced-blocks)
                               #'<
                               :key (lambda (block)
                                      (block-header-number
                                       (block-header block)))))
            (engine-payload-store-notify-log-filters
             store
             block
             :removed-p t))
          (dolist (block (sort (copy-list path)
                               #'<
                               :key (lambda (block)
                                      (block-header-number
                                       (block-header block)))))
            (engine-payload-store-notify-log-filters store block))))
      (engine-payload-store-remove-new-head-invalid-txpool-transactions
       store
       :chain-config chain-config)
      (engine-payload-store-revalidate-pending-transactions
       store
       :expected-chain-id expected-chain-id)
      (engine-payload-store-promote-queued-transactions
       store
       nil
       :expected-chain-id expected-chain-id)
      (engine-payload-store-promote-basefee-and-queued-transactions
       store
       :expected-chain-id expected-chain-id)
      (when head-changed-p
        (engine-payload-store-notify-block-filters store head-block))
      head-block)))
